	;; PC-98 シリーズ用 .FDD イメージ作成ツール
	;; 
	;; DA/UA 10H のドライブのフロッピーディスクを読み込み，
	;; .FDD ファイルを作成します。
	;;
	;; 長いファイル名は使えないため，.FDD.??? は .??? と作成します。
	;;
	;; ファイル名は TEMP.FDD です。

TEXT	CSEG

	RS	100H

START:	MOV	DX,MES1
	MOV	AH,9
	INT	21H
	MOV	AH,1
	INT	21H
	MOV	SI,AX
	MOV	DX,FILE1
	XOR	CX,CX
	MOV	AH,3CH
	INT	21H
	MOV	DX,MES2
	JC	ERR1
	MOV	BX,AX
	;; ---- FIRST, SAVE 512 BYTES SECTORS
	MOV	DX,1		; INITIALIZE REGISTERS
	MOV	CX,200H
	;; SEEK TO CORRECT CYLINDER
L2:	PUSH	DX
	PUSH	CX
	TEST	SI,1
	JZ	L1
	SHL	CL,1
L1:	MOV	AX,1010H
	INT	1BH
	POP	CX
	MOV	DX,MES3
	JC	ERR1
	MOV	DL,'.'
	MOV	AH,2
	INT	21H
	POP	DX
	;; READ DATA TO THE BUFFER
L3:	PUSH	BX
	MOV	BX,512
	MOV	AX,4610H
	CALL	READBUF
	POP	BX
	;; WRITE DATA TO THE FILE
	PUSH	DX
	PUSH	CX
	MOV	DX,BP
	MOV	CX,512
	MOV	AH,40H
	INT	21H
	MOV	DX,MES4
	JC	ERR1
	POP	CX
	POP	DX
	;; CONTINUE READING
	INC	DL
	CMP	DL,10
	JNE	L3
	MOV	DL,1
	INC	DH
	CMP	DH,2
	JNE	L3
	MOV	DH,0
	INC	CL
	MOV	AL,80
	TEST	SI,1
	JZ	L4
	MOV	AL,40
L4:	CMP	CL,AL
	JNE	L2
	;; FINISH
	MOV	AH,3EH
	INT	21H

	MOV	DL,13
	MOV	AH,2
	INT	21H
	MOV	DL,10
	MOV	AH,2
	INT	21H

	;; ---- SECOND, SAVE 1024 BYTES SECTORS
	;; SEARCH FOR 1024 BYTES SECTOR
	MOV	CX,300H		; INITIALIZE REGISTERS

L12:	MOV	DL,'.'
	MOV	AH,2
	INT	21H
	PUSH	CX
	TEST	SI,1
	JZ	L5
	SHL	CL,1
L5:	MOV	AX,1010H	; SEEK TO CORRECT SECTOR
	INT	1BH
	POP	CX
	MOV	DX,MES3
	JC	ERR1

	MOV	DX,1
L7:	MOV	BX,1024
	MOV	AX,4610H
	CALL	READBUF
	JC	L6		; - THIS TRACK DOESN'T HAVE 1024 BYTES SECTOR

	;; FOUND. NEXT, READ AND SAVE THE SECTORS
	PUSH	DX
	PUSH	CX
	MOV	DX,FILE2
	XOR	CX,CX
	MOV	AH,3CH
	INT	21H
	MOV	DX,MES2
	JC	ERR1
	MOV	BX,AX
L10:	MOV	DX,BP
	MOV	CX,1024
	MOV	AH,40H
	INT	21H
	MOV	DX,MES4
	JC	ERR1
	POP	CX
	POP	DX
	INC	DL
	CMP	DL,6
	JE	L9
	PUSH	BX
	MOV	BX,1024
	MOV	AX,4610H
	CALL	READBUF
	POP	BX
	PUSH	DX
	PUSH	CX
	JMP	L10

L9:	MOV	DL,0
	;; CLOSE THE FILE
	MOV	AH,3EH
	INT	21H

L6:	INC	[FILE2E+2].B
	CMP	[FILE2E+2].B,'9'+1
	JNE	L8
	MOV	[FILE2E+2].B,'0'
	INC	[FILE2E+1].B
	CMP	[FILE2E+1].B,'9'+1
	JNE	L8
	MOV	[FILE2E+1].B,'0'
	INC	[FILE2E+0].B
L8:	INC	DH
	CMP	DH,2
	JNE	L7
	INC	CL
	MOV	AL,80
	TEST	SI,1
	JZ	L11
	MOV	AL,40
L11:	CMP	CL,AL
	JNE	L12

	;; COMPLETE
	MOV	DX,MES5
ERR1:	MOV	AH,9
	INT	21H
	INT	20H

READBUF:
	CALL	SUB1
	JC	READBUF_3
	PUSH	DI
	MOV	DI,AX
READBUF_2:
	MOV	BP,BUF
	MOV	AX,DI
	INT	1BH
	PUSHF
	JNC	READBUF_1
	CMP	AH,20H
	JNE	READBUF_1
	POPF
	ADD	BP,BX
	JMP	READBUF_2
READBUF_1:
	POPF
	POP	DI
READBUF_3:
	RET
SUB1:	; BEFORE READING, FIND THE SECTOR WHICH IS GOING TO BE READED
	PUSH	AX
	PUSH	BX
	PUSH	CX
	PUSH	DX
	PUSH	SI
	PUSH	DI
	MOV	SI,CX		; SAVE 'N' AND 'C'
	MOV	DI,DX		; SAVE 'H' AND 'R'

	MOV	AX,4A10H	; READ ID
	INT	1BH
	JC	SUB1_R		; IF ERROR, DIRECTLY RETURN

	PUSH	CX		; SAVE
	PUSH	DX
SUB1_3:	CMP	CX,SI
	JNE	SUB1_1
	CMP	DX,DI
	JNE	SUB1_1
	CLC			; IF FOUND, CLEAR CARRY FLAG AND RETURN
	POP	DX
	POP	CX
	JMP	SUB1_R
SUB1_1:	MOV	CX,SI
	MOV	DX,DI
	MOV	AX,4A10H	; READ ID
	INT	1BH
	JC	SUB1_R		; IF ERROR, DIRECTLY RETURN
	POP	BX
	POP	AX
	CMP	AX,CX
	JNE	SUB1_2
	CMP	BX,DX
	JNE	SUB1_2
	STC
	JMP	SUB1_R
SUB1_2:	PUSH	AX
	PUSH	BX
	JMP	SUB1_3

SUB1_R:	POP	DI
	POP	SI
	POP	DX
	POP	CX
	POP	BX
	POP	AX
	RET

MES1:	DB	'40 シリンダフォーマットの場合は 1',13,10
	DB	'80 シリンダの場合は 2 とタイプしてください。',13,10
	DB	'$'
MES2:	DB	'ファイルを作成できません',13,10
	DB	'$'
MES3:	DB	'SEEK ERROR',13,10
	DB	'$'
MES4:	DB	'WRITE ERROR',13,10
	DB	'$'
MES5:	DB	13,10
	DB	'COMPLETED',13,10
	DB	'$'
FILE1:	DB	'TEMP.FDD',0
FILE2:	DB	'TEMP.'
FILE2E:	DB	'000',0
BUF:	RS	16384

	END	START
